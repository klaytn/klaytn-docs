# M·∫°ng l∆∞·ªõi Orakl

## Gi·ªõi thi·ªáu

![](/img/build/tools/klaytnXorakl.png)

[M·∫°ng Orakl](https://docs.orakl.network/docs/developers-guide/readme) l√† m·ªôt m·∫°ng l∆∞·ªõi oracle phi t·∫≠p trung cho ph√©p c√°c h·ª£p ƒë·ªìng th√¥ng minh truy c·∫≠p d·ªØ li·ªáu ngo√†i chu·ªói v√† c√°c t√†i nguy√™n kh√°c m·ªôt c√°ch an to√†n. It prides itself in being a native token oracle that provides [Data Feed](https://docs.orakl.network/developers-guide/data-feed), [VRF](https://docs.orakl.network/developers-guide/vrf), [Request-Response](https://docs.orakl.network/developers-guide/request-response) and [Proof of Reserve](https://docs.orakl.network/developers-guide/proof-of-reserve) solutions.

V·ªõi M·∫°ng Orakl, ng∆∞·ªùi d√πng c√≥ th·ªÉ t√¨m ngu·ªìn ng·∫´u nhi√™n kh√¥ng th·ªÉ ƒëo√°n tr∆∞·ªõc v√† kh√¥ng thi√™n v·ªã trong h·ª£p ƒë·ªìng th√¥ng minh c·ªßa h·ªç. M·∫°ng Orakl [Verifiable Randon Fuction (VRF)](https://docs.orakl.network/docs/developers-guide/verifiable-random-function-vrf#what-is-verifiable-random-function) cho ph√©p c√°c h·ª£p ƒë·ªìng th√¥ng minh s·ª≠ d·ª•ng VRF ƒë·ªÉ t·∫°o ra c√°c gi√° tr·ªã ng·∫´u nhi√™n r√µ r√†ng, c√≥ th·ªÉ ƒë∆∞·ª£c s·ª≠ d·ª•ng trong c√°c dApp kh√°c nhau ƒë√≤i h·ªèi t√≠nh ng·∫´u nhi√™n. Orakl Network provides developers access to the VRF services through two different account types, namely: [Permanent Account](https://docs.orakl.network/developers-guide/readme#permanent-account) or [Temporary Account](https://docs.orakl.network/developers-guide/readme#temporary-account).

Trong h∆∞·ªõng d·∫´n n√†y, b·∫°n s·∫Ω s·ª≠ d·ª•ng ch·ª©c nƒÉng VRF t·ª´ M·∫°ng l∆∞·ªõt Orakl ƒë·ªÉ y√™u c·∫ßu c√°c s·ªë ng·∫´u nhi√™n trong h·ª£p ƒë·ªìng th√¥ng minh c·ªßa b·∫°n.

## ƒêi·ªÅu ki·ªán ti√™n quy·∫øt

- [Kaikas](https://chrome.google.com/webstore/detail/kaikas/jblndlipeogpafnldhgmapagcccfchpi?hl=en)
- [Remix IDE](https://remix.ethereum.org/)
- [Plugin Klaytn tr√™n b·∫£n ph·ªëi l·∫°i](https://klaytn.foundation/using-klaytn-plugin-on-remix/)
- Ki·ªÉm tra KLAY t·ª´ [Faucet](https://baobab.wallet.klaytn.foundation/faucet)

## B·∫Øt ƒë·∫ßu

In the following steps, you will request for a random word in your smart contract using Orakl Network. H√£y b·∫Øt ƒë·∫ßu th√¥i!

### Step 1: Initialize Contract State Variables

In this step, we will define the cosumer contract and initialize the state variables needed for our contract functionality. Our consumer contract is dependent on `VRFConsumerBase` contract from which we inherit, and `IVRFCoordinator` interface that is used for calls to `VRFCoordinator` contract. Next, we define `sRandomWord` variable which we use to store the random word result and the `sOwner` variable which is used inside of `onlyOwner` modifier.

```solidity
pragma solidity ^0.8.16;

import { VRFConsumerBase } from "@bisonai/orakl-contracts/src/v0.1/VRFConsumerBase.sol";
import { IVRFCoordinator } from "@bisonai/orakl-contracts/src/v0.1/interfaces/IVRFCoordinator.sol";

contract VRFConsumer is VRFConsumerBase {
  uint256 public sRandomWord;
  address private sOwner;

  error OnlyOwner(address notOwner);
  modifier onlyOwner() {
      if (msg.sender != sOwner) {
          revert OnlyOwner(msg.sender);
      }
      _;
  }
```

### Step 2: Initialize VRF Coordinator

To request for random words in your smart contract, you need to initialize the [`VRFCoordinator`](https://github.com/Bisonai/orakl/blob/master/contracts-v0.1/src/v0.1/VRFCoordinator.sol) smart contract. It is recommended to bond `VRFCoordinator` interface with `VRFCoordinator` address supplied through a constructor parameter, and use it for random word requests (`requestRandomWords`). The `VRFCoordinator` contract is deployed both on Klaytn Baobab [0xDA8c0A00A372503aa6EC80f9b29Cc97C454bE499](https://baobab.klaytnfinder.io/account/0xDA8c0A00A372503aa6EC80f9b29Cc97C454bE499) and Klaytn Cypress [0x3F247f70DC083A2907B8E76635986fd09AA80EFb](https://www.klaytnfinder.io/account/0x3F247f70DC083A2907B8E76635986fd09AA80EFb).

```solidity
  IVRFCoordinator COORDINATOR;

  constructor(address coordinator) VRFConsumerBase(coordinator) {
      COORDINATOR = IVRFCoordinator(coordinator);
      sOwner = msg.sender;
  }
```

### Step 3: Request Random Words with Temporary Account

To request random words with a temporary account, users need to send $KLAY together with a call using value property.

```solidity
  function requestRandomWordsDirect(
      bytes32 keyHash,
      uint32 callbackGasLimit,
      uint32 numWords,
      address refundRecipient
  )
      public
      payable
      onlyOwner
      returns (uint256 requestId)
  {
    requestId = COORDINATOR.requestRandomWords{value: msg.value}(
      keyHash,
      callbackGasLimit,
      numWords,
      refundRecipient
    );
  }
```

This function calls the `requestRandomWords()` function defined in `COORDINATOR` contract, and passes `keyHash`, `callbackGasLimit`, `numWords` and `refundRecipient` as arguments. The payment for service is sent through `msg.value` to the `requestRandomWords()` in `COORDINATOR` contract. If the payment is larger than expected payment, exceeding payment is returned to the `refundRecipient` address. Eventually, it generates a request for random words. To accurately specify `msg.value` for the `requestRandomWords` function, please refer to the explanation on [how to estimate the service fee](https://docs.orakl.network/developers-guide/vrf#get-estimated-service-fee).

### B∆∞·ªõc 4: Th·ª±c hi·ªán c√°c t·ª´ ng·∫´u nhi√™n

The `fulfillRandomWords` function is called by `VRFCoordinator` contract when fulfilling the random words request.

```solidity
function fulfillRandomWords(
    uint256 /* requestId */,
    uint256[] memory randomWords
)
    internal
    override
{
    // requestId should be checked if it matches the expected request
    // Generate random value between 1 and 50.
    sRandomWord = (randomWords[0] % 50) + 1;
}
```

B√¢y gi·ªù ch√∫ng ta ƒë√£ c√≥ m√£ gi·∫£i ph√°p Orakl VRF, h√£y c≈©ng xem n√≥ ho·∫°t ƒë·ªông nh∆∞ th·∫ø n√†o.

## Th·ª±c hi·ªán th·ª±c t·∫ø

In the example below, the contract allows us to request for random words and receive its fulfillment.

### T·∫°o v√† Tri·ªÉn Khai M√£ M·∫´u

**Remix IDE**

- ƒêi·ªÅu h∆∞·ªõng ƒë·∫øn [Remix IDE](https://remix.ethereum.org/).
- Click on the **File Explorer** tab, create a new file named `consumer-vrf.sol` in the contracts folder.
- D√°n m√£ b√™n d∆∞·ªõi trong t·ªáp m·ªõi ƒë∆∞·ª£c t·∫°o.
- Trong Remix, nh·∫•p v√†o **H·ª£p ƒê·ªìng Bi√™n D·ªãch**.
- Nh·∫•p v√†o tab Klaytn b√™n tr√°i c·ªßa b·∫°n sau khi c√†i ƒë·∫∑t plugin.
- Ch·ªçn **M√¥i Tr∆∞·ªùng** &gt; **ƒê∆∞·ª£c Ti√™m Caver** - **Kaikas**.
- Trong h·ª£p ƒë·ªìng, ch·ªçn h·ª£p ƒë·ªìng c·ªßa b·∫°n. V√≠ d·ª•, VRFConsumer.
- Truy·ªÅn trong ƒë·ªãa ch·ªâ h·ª£p ƒë·ªìng ƒëi·ªÅu ph·ªëi vi√™n `0xDA8c0A00A372503aa6EC80f9b29Cc97C454bE499` (Baobab), `0x3F247f70DC083A2907B8E76635986fd09AA80EFb` (Cypress).
- Nh·∫•p v√†o **Tri·ªÉn Khai**.

**M√£ M·∫´u**

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import {VRFConsumerBase} from "@bisonai/orakl-contracts/src/v0.1/VRFConsumerBase.sol";
import {IVRFCoordinator} from "@bisonai/orakl-contracts/src/v0.1/interfaces/IVRFCoordinator.sol";

contract VRFConsumer is VRFConsumerBase {
    uint256 public sRandomWord;
    address private sOwner;

    IVRFCoordinator COORDINATOR;

    error OnlyOwner(address notOwner);

    modifier onlyOwner() {
        if (msg.sender != sOwner) {
            revert OnlyOwner(msg.sender);
        }
        _;
    }

    constructor(address coordinator) VRFConsumerBase(coordinator) {
        sOwner = msg.sender;
        COORDINATOR = IVRFCoordinator(coordinator);
    }

    function requestRandomWordsDirect(
        bytes32 keyHash,
        uint32 callbackGasLimit,
        uint32 numWords,
        address refundRecipient
    ) public payable onlyOwner returns (uint256 requestId) {
        requestId = COORDINATOR.requestRandomWords{value: msg.value}(
            keyHash,
            callbackGasLimit,
            numWords,
            refundRecipient
        );
    }

    function fulfillRandomWords(
        uint256 /* requestId */,
        uint256[] memory randomWords
    ) internal override {
        // requestId should be checked if it matches the expected request
        // Generate random value between 1 and 50.
        sRandomWord = (randomWords[0] % 50) + 1;
    }
}
```

![](/img/build/tools/orakl-vrf-deploy.png)

### T∆∞∆°ng t√°c v·ªõi H·ª£p ƒê·ªìng Th√¥ng Minh

To request for random words in your smart contract, you have to first execute the `requestRandomWordsDirect()` function. For this function to successfully execute, the user has to send KLAY (minimum of 1 KLAY) as stated previously, and supply `keyHash`, `callbackGasLimit`, `numWords`, and `refundRecipient` parameters. `keyHash` parameter uniquely defines who can fulfill the request. Orakl Network VRF provides one key hash for each Klaytn chain:

- Baobab: `0xd9af33106d664a53cb9946df5cd81a30695f5b72224ee64e798b278af812779c`
- Cypress: `0x6cff5233743b3c0321a19ae11ab38ae0ddc7ddfe1e91b162fa8bb657488fb157`

For the rest of the parameters, you can set them as follows:

- `callbackGasLimit` as `500000`,
- `numWords` as `1`, and
- set `refundRecipient` to your EOA address.

Afterwards, once the request has been fulfilled, the `sRandomWord()` function can be executed. This `sRandomWord()` function returns the random word.

- **requestRandomWordsDirect()**: Will be sending 1 KLAY to execute this function. The image below illustrate this:

![](/img/build/tools/orakl-vrf-request.png)

- **sRandomWord()**: After the `VRFCoordinator` has fulfilled the random word request, the response is stored in the `sRandomWord` variable. To get the response, call the `sRandomWord()` function.

![](/img/build/tools/orakl-vrf-response.png)

Tada üéâ! You just requested for a random word and received one in your smart contract.

## Ph·∫ßn k·∫øt lu·∫≠n

In this tutorial, you learnt how to generate a random word in your smart contract using the Orakl Network VRF solution. The Orakl Network provides more oracle services such as Data Feed, Request-Response, Proof of Reserve. For more in-depth guides on Orakl Network and how it works, please refer to the [Orakl Network documentation](https://docs.orakl.network).
