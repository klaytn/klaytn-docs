# 4. Klaystagram 스마트 컨트랙트 작성 <a id="4-write-klaystagram-smart-contract"></a>

1\) Background  
2\) Contract setup  
3\) Set events and data structure  
4\) Write functions

* `uploadPhoto`
* `transferOwnership`
* `getPhoto`

## 1\) 배경지식 <a id="1-background"></a>

우리는 "Klaystagram"이라는 간단한 컨트랙트를 작성할 것입니다.

* `PhotoData` 구조체는 다양한 사진 데이터를 저장하도록 정의됩니다.
* `uploadPhoto`와 `transferOwnership` 함수를 통해 사진을 업로드하고 사진의 소유권을 이전할 수 있습니다.

## 2\) 스마트 컨트랙트 설정 <a id="2-contract-setup"></a>

* 솔리디티 버전을 지정하세요. 0.5.6 stable 버전을 권장합니다.
* NFT을 구현하기 위해 ERC721 표준을 사용할 것입니다.
  * `ERC721.sol`와 `ERC721Enumerable.sol`를 임포트하세요.
  * ERC721에 대한 자세한 설명은 [erc721.org](http://erc721.org)을 참고하세요.

```text
pragma solidity 0.5.6;

import "./ERC721/ERC721.sol";
import "./ERC721/ERC721Enumerable.sol";

contract Klaystagram is ERC721, ERC721Enumerable {
```

## 3\) 이벤트 및 데이터 구조 설정 <a id="3-set-events-and-data-structure"></a>

블록체인에서 일어나는 활동을 추적하기 위해 이벤트를 설정해야 합니다.

데이터 구조는 `_photoList` 맵핑을 이용하여 uint256 자료형의 `tokenId`를 `PhotoData` 구조체로 연관시킵니다. PhotoUploaded 이벤트를 정의하여 이 이벤트가 포함된 함수가 호출될 때마다 트랜잭션 영수증에 기록되도록 합니다.

```text
event PhotoUploaded (uint indexed tokenId, bytes photo, string title, string location, string description, uint256 timestamp);

mapping (uint256 => PhotoData) private _photoList;

struct PhotoData {
    uint256 tokenId;                       // 1부터 차례로 1씩 증가하는 고유한 토큰 ID
    address[] ownerHistory;                // 이전 소유자들의 기록
    bytes photo;                           // 이미지의 소스
    string title;                          // 사진 제목
    string location;                       // 사진 촬영 장소
    string description;                    // 사진에 대한 간략한 설명
    uint256 timestamp;                     // 업로드된 시간
}
```

## 4\) 함수 작성 <a id="4-write-functions"></a>

컨트랙트와 상호작용하는 함수를 작성해봅시다. 본 튜토리얼에서는 `uploadPhoto`와 `transferOwnership` 두 함수만 고려할 것입니다. 모든 함수를 보려면 Klaystagram.sol을 확인해주세요.

### `uploadPhoto` <a id="uploadphoto"></a>

`uploadPhoto` 함수는 사진의 이미지 소스를 포함하여 4개의 인자를 입력으로 받습니다. 설명의 편의를 위해 `tokenId`는 단순히 1부터 차례로 1씩 증가하도록 하겠습니다.

`_mint`는 ERC721 표준 컨트랙트의 함수입니다. 이 함수는 새 토큰을 만들고 특정 주소에 토큰을 할당합니다. 여기서는 `msg.sender`에 할당하도록 하겠습니다. 이 애플리케이션에서 로그인 상태의 사용자는 본인의 개인키로 트랜잭션을 작성하게 됩니다. 그러므로 `msg.sender`는 해당 사용자의 공개키 주소가 될 것입니다.

마지막으로 `PhotoData` 구조체를 초기화하고 `_photoList` 맵핑 내에 위치시킨 후 소유자의 주소를 `ownerHistory` 배열에 넣으세요. 그리고 위에서 만든 PhotoUploaded 이벤트를 포함하는 것도 잊지 마세요. 이전에 언급했듯이 이 이벤트는 트랜잭션 영수증에 포함될 것입니다.

```text
function uploadPhoto(bytes memory photo, string memory title, string memory location, string memory description) public {
    uint256 tokenId = totalSupply() + 1;

    _mint(msg.sender, tokenId);

    address[] memory ownerHistory;

    PhotoData memory newPhotoData = PhotoData({
        tokenId : tokenId,
        ownerHistory : ownerHistory,
        photo : photo,
        title: title,
        location : location,
        description : description,
        timestamp : now
    });

    _photoList[tokenId] = newPhotoData;
    _photoList[tokenId].ownerHistory.push(msg.sender);

    emit PhotoUploaded(tokenId, photo, title, location, description, now);
}
```

### `transferOwnership` <a id="transferownership"></a>

`transferOwnership` 함수를 작성할 차례입니다. 사진의 소유권을 이전할 때 두 가지 작업이 필요합니다. 먼저 소유자를 다시 지정하고 새로운 소유자의 주소를 `ownerHistory` 배열에 넣어야 합니다.

`transferOwnership`이 ERC721 표준의 `safeTransferFrom` 함수를 호출하고 그 다음으로 `transferFrom` 함수를 호출하도록 하여 이 작업을 할 수 있습니다. 앞서 언급하였듯이 토큰 전송이 완료된 직후에 `ownerHistory` 배열에 새로운 소유자의 정보를 넣어야 하므로 아래와 같이 `transferFrom` 함수를 재정의하였습니다.

```text
/**
  * @notice safeTransferFrom 함수는 수신자가 ERC721 토큰을 처리할 수 있는지 체크하여 토큰이 유실되는 상황을 줄여줍니다. 해당 사실을 확인한 후 아래에서 정의된 transferFrom 함수를 호출합니다.
  */
function transferOwnership(uint256 tokenId, address to) public returns(uint, address, address, address) {
    safeTransferFrom(msg.sender, to, tokenId);
}

/**
  * @notice safeTransferFrom 함수를 사용하는 transferOwnership을 사용하기를 권장합니다.
  * @dev 소유권이 이전될 때마다 새로운 소유자의 주소가 ownerHistory 배열에 추가되도록 하기 위해 transferFrom 함수를 재정의합니다.
  */
function transferFrom(address from, address to, uint256 tokenId) public {
    super.transferFrom(from, to, tokenId);
    _photoList[tokenId].ownerHistory.push(to);
}
```

### `getPhoto` <a id="getphoto"></a>

마지막으로 스마트 컨트랙트에 저장된 데이터를 불러오는 함수를 만들어봅시다. 하나의 함수를 호출하여 특정 사진에 관련된 모든 정보를 한번에 가져올 것입니다. 그러므로 `getPhoto` 함수는 인덱스\(토큰 ID\)를 인자로 받아 PhotoData 구조체의 모든 요소를 반환해야 합니다.

```text
function getPhoto(uint tokenId) public view
returns(uint256, address[] memory, bytes memory, string memory, string memory, string memory, uint256) {
    require(_photoList[tokenId].tokenId != 0, "Photo does not exist");
    return (
        _photoList[tokenId].tokenId,
        _photoList[tokenId].ownerHistory,
        _photoList[tokenId].photo,
        _photoList[tokenId].title,
        _photoList[tokenId].location,
        _photoList[tokenId].description,
        _photoList[tokenId].timestamp);
}
```

수고하셨습니다! 이제 작성한 컨트랙트를 배포할 수 있습니다.

